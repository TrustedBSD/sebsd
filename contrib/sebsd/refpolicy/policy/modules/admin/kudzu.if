## <summary>Hardware detection and configuration tools</summary>

########################################
## <summary>
##	Execute kudzu in the kudzu domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
interface(`kudzu_domtrans',`
	gen_require(`
		type kudzu_t, kudzu_exec_t;
	')

	domain_auto_trans($1,kudzu_exec_t,kudzu_t)

	allow $1 kudzu_t:fd use;
	allow kudzu_t $1:fd use;
	allow kudzu_t $1:fifo_file rw_file_perms;
	allow kudzu_t $1:process sigchld;
')

########################################
## <summary>
##	Execute kudzu in the kudzu domain, and
##	allow the specified role the kudzu domain.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	The role to be allowed the kudzu domain.
##	</summary>
## </param>
## <param name="terminal">
##	<summary>
##	The type of the terminal allow the kudzu domain to use.
##	</summary>
## </param>
#
interface(`kudzu_run',`
	gen_require(`
		type kudzu_t;
	')

	kudzu_domtrans($1)
	role $2 types kudzu_t;
	allow kudzu_t $3:chr_file rw_term_perms;
')

########################################
## <summary>
##	Get attributes of kudzu executable.
## </summary>
## <param name="domain">
##	<summary>
##	The type of the process performing this action.
##	</summary>
## </param>
#
# cjp: added for ddcprobe
interface(`kudzu_getattr_exec_files',`
	gen_require(`
		type kudzu_exec_t;
	')

	allow $1 kudzu_exec_t:file getattr;
')
